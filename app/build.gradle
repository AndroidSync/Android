apply plugin:'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'com.google.firebase.firebase-perf'

apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('../play-service-account.json')
        }
    }

    signingConfigs {
        release {
            keyAlias 'zyncco'
            keyPassword keystoreProperties["KEY_PASS"].toString()
            storeFile file('../keystore.jks')
            storePassword keystoreProperties["KEYSTORE_PASS"].toString()
        }
        debug {
            keyAlias 'zyncco'
            keyPassword keystoreProperties["KEY_PASS"].toString()
            storeFile file('../keystore.jks')
            storePassword keystoreProperties["KEYSTORE_PASS"].toString()
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "co.zync.zync"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    buildTypes.each {
        it.resValue "string", "facebook_application_id", keystoreProperties["FacebookApplicationId"].toString()
        it.resValue "string", "facebook_login_protocol_scheme", "fb" + keystoreProperties["FacebookApplicationId"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'net.gotev:uploadservice:3.2.3'
    compile 'com.google.android.gms:play-services-auth:10.2.6'
    compile 'com.google.firebase:firebase-core:10.2.6'
    compile 'com.google.firebase:firebase-messaging:10.2.6'
    compile 'com.google.firebase:firebase-auth:10.2.6'
    compile 'com.google.firebase:firebase-perf:10.2.6'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.android.support:cardview-v7:21.0.+'
    compile 'com.android.support:recyclerview-v7:21.0.+'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    testCompile 'junit:junit:4.12'
}

play {
    track = 'alpha'
}

apply plugin: 'com.google.gms.google-services'